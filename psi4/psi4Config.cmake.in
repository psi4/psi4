# psi4Config.cmake
# ----------------
#
# Psi4 cmake module.
# This module sets the following variables in your project::
#
#   psi4_FOUND - true if psi4 and all required components found on the system
##   psi4_VERSION - psi4 version in format Major.Minor.Release
##   psi4_INCLUDE_DIRS - Directory where psi4 header is located.
#   psi4_INCLUDE_DIR - Directory where psi4/psi4-dec.h is located.
##   psi4_DEFINITIONS - Definitions necessary to use psi4, namely USING_psi4.
##   psi4_LIBRARIES - psi4 library to link against.
#   psi4_LIBRARY - core@PYTHON_MODULE_EXTENSION@ library to link against.
#   psi4_EXECUTABLE - psi4 executable script.
##   psi4_FRAGLIB_DIRS - Directories (list) where EFP fragments are located
#
#
# Available components::
#
#   ambit - tensor library active
#   chemps2 - dmrg library active
#   dkh - relativistic library active
#   erd - integrals library active
#   gdma - multipole library active
#   pcmsolver - solvation library active
#   simint - integrals library active
#
#
# Exported targets::
#
# If psi4 is found, this module defines the following :prop_tgt:`IMPORTED`
# target. ::
#
#   psi4::core - the main psi4 library with header & pybind11 & Python headers attached.
#
#
# Suggested usage::
#
#   find_package(psi4)
##   find_package(psi4 1.3.0 EXACT CONFIG REQUIRED COMPONENTS shared)
#
#
# The following variables can be set to guide the search for this package::
#
#   psi4_DIR - CMake variable, set to directory containing this Config file
#   CMAKE_PREFIX_PATH - CMake variable, set to root directory of this package
#   PATH - environment variable, set to bin directory of this package
#   CMAKE_DISABLE_FIND_PACKAGE_psi4 - CMake variable, disables
#     find_package(psi4) when not REQUIRED, perhaps to force internal build

@PACKAGE_INIT@

set(PN psi4)
set (_valid_components
    ambit
    chemps2
    dkh
    erd
    gdma
    pcmsolver
    simint
)

# location of script psi4
set(${PN}_EXECUTABLE "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/psi4")

# directory containing psi4/psi4-dec.h
set(${PN}_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@")

# location of psi4/core.so
set(${PN}_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@@PYMOD_INSTALL_LIBDIR@/psi4/@PYTHON_MODULE_PREFIX@core@PYTHON_MODULE_EXTENSION@")
set(${PN}_SYS_PATH "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@@PYMOD_INSTALL_LIBDIR@")

# presence of external addons
macro(psi4_component name on)
    if(${on})
        set(${PN}_${name}_FOUND 1)
        list(APPEND ${PN}_FOUND_COMPONENTS ${name})
    endif()
endmacro()

psi4_component(ambit @ENABLE_ambit@)
psi4_component(chemps2 @ENABLE_CheMPS2@)
psi4_component(dkh @ENABLE_dkh@)
psi4_component(erd @ENABLE_erd@)
psi4_component(gdma @ENABLE_gdma@)
psi4_component(pcmsolver @ENABLE_PCMSolver@)
psi4_component(cppe @ENABLE_cppe@)
psi4_component(simint @ENABLE_simint@)

foreach(_comp ${${PN}_FIND_COMPONENTS})
    if(${${PN}_${_comp}_FOUND})
    else()
        if(NOT CMAKE_REQUIRED_QUIET)
            message(STATUS "${PN} missing requested component: ${_comp}")
        endif()
    endif()
endforeach()

check_required_components(${PN})

if(NOT CMAKE_REQUIRED_QUIET)
    message(STATUS "Psi4 script:   ${${PN}_EXECUTABLE}")
    message(STATUS "Psi4 headers:  ${${PN}_INCLUDE_DIR}")
    message(STATUS "Psi4 library:  ${${PN}_LIBRARY}")
    message(STATUS "Psi4 sys.path: ${${PN}_SYS_PATH}")
    message(STATUS "Psi4 components: ${${PN}_FOUND_COMPONENTS}")
    message(STATUS "Python executable: @Python_EXECUTABLE@")
endif()

#-----------------------------------------------------------------------------
# Don't include targets if this file is being picked up by another
# project which has already built this as a subproject
#-----------------------------------------------------------------------------
if(NOT TARGET ${PN}::core)
    include("${CMAKE_CURRENT_LIST_DIR}/${PN}Targets.cmake")
    set(CMAKE_CXX_STANDARD @CMAKE_CXX_STANDARD@)
    if (${CMAKE_MINIMUM_REQUIRED_VERSION} VERSION_LESS 3.15)
        message(WARNING "Calling program/plugin using old Python detection because cmake_minimum_required (${CMAKE_MINIMUM_REQUIRED_VERSION}) is <3.15. Contact the program/plugin developer to update.")
    else ()
        find_package(Python 3.6 COMPONENTS Interpreter Development NumPy REQUIRED)
    endif()
    find_package(pybind11 CONFIG REQUIRED)
    if(NOT DEFINED ENABLE_OPENMP)
      set(ENABLE_OPENMP "@ENABLE_OPENMP@")
    endif()
    find_package(TargetLAPACK CONFIG REQUIRED)
    find_package(Libint2 CONFIG REQUIRED)
    # No more ${PN} since defined in above find_package
endif()

function(add_psi4_plugin TARGET SOURCES)
    # remove ${TARGET} from ${ARGV} to use ${ARGV} as SOURCES
    list(REMOVE_AT ARGV 0)

    set(psi4_CXX_STANDARD @CMAKE_CXX_STANDARD@)
    # checks compiler (and gcc, if necessary) C++14 compliant
    include(custom_cxxstandard)

    add_library(${TARGET} MODULE ${ARGV})
    target_link_libraries(${TARGET} PRIVATE psi4::core)
    target_link_libraries(${TARGET} PRIVATE tgt::MathOpenMP)
    target_include_directories(${TARGET} PRIVATE "${Python_INCLUDE_DIRS}")
    target_include_directories(${TARGET} PRIVATE "${PYTHON_INCLUDE_DIRS}")
    set_target_properties(${TARGET} PROPERTIES PREFIX "")
    if(APPLE)
      set_target_properties(${TARGET} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
    endif()
endfunction()

# make detectable the various cmake modules exported by psi4
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
